# vi: set ft=gnuplot :

set title font "Helvetica,20"
set key left top Left

set terminal postscript color eps enhanced 22

set size 1,0.65


filename='graphs/'.workload.'-'.type.'.eps'
set output filename

set style data histogram
set style histogram rowstacked
#set style fill pattern border -1
set style fill solid border -1
set boxwidth 0.9
set xtics format ""
set grid ytics
set style line 2 lc rgb 'black' lt 1 lw 1

set key autotitle columnheader

ylabel=labely
set ylabel ylabel
set yrange [0:maxvalue]

set xlabel "Number of client threads"

set xtics offset -3

input16='data/'.type.'-workload'.workload.'-16.dat'
input32='data/'.type.'-workload'.workload.'-32.dat'
input64='data/'.type.'-workload'.workload.'-64.dat'
input128='data/'.type.'-workload'.workload.'-128.dat'
input256='data/'.type.'-workload'.workload.'-256.dat'
set label 1 "Cassandra" at 0,cass rotate by 90 front font "Helvetica,15"
set label 2 "SconeKV" at 1,scone rotate by 90 front font "Helvetica,15"
set label 3 "CockroachDB" at 2,roach rotate by 90 front font "Helvetica,15"
set style line 1 lc rgb 'gray10' lt 1 lw 2
set style line 2 lc rgb 'gray30' lt 1 lw 2
set style line 3 lc rgb 'gray40' lt 1 lw 2
set style line 4 lc rgb 'gray70' lt 1 lw 2
set style line 5 lc rgb 'gray90' lt 1 lw 2
set style line 6 lc rgb 'black' lt 1 lw 2
plot \
    newhistogram "16", input16 u 2 ls 1, '' u 3 ls 2, '' u 4 ls 3, '' u 5 ls 4, '' u 6 ls 5, \
    newhistogram "32", input32 u 2 notitle ls 1, '' u 3 notitle ls 2, '' u 4 notitle ls 3, '' u 5 notitle ls 4, '' u 6 notitle ls 5, \
    newhistogram "64", input64 u 2 notitle ls 1, '' u 3 notitle ls 2, '' u 4 notitle ls 3, '' u 5 notitle ls 4, '' u 6 notitle ls 5, \
    newhistogram "128", input128 u 2 notitle ls 1, '' u 3 notitle ls 2, '' u 4 notitle ls 3, '' u 5 notitle ls 4, '' u 6 notitle ls 5, \
    newhistogram "256", input256 u 2 notitle ls 1, '' u 3 notitle ls 2, '' u 4 notitle ls 3, '' u 5 notitle ls 4, '' u 6 notitle ls 5
